const N = 3
range Nodes= 0..N-1

NODE(I=N) = IDLE,
LEADER    = ( open -> close -> send.token -> IDLE
			| send.token -> IDLE),
IDLE 	  = ( recv.token -> LEADER
	   		| recv.claim[i:Nodes] ->
				if (i < I) then (send.claim[i] -> IDLE)
				else IDLE
	   		| send.claim[I] -> ELIGIBLE),
ELIGIBLE  = ( recv.token -> LEADER
		   	| recv.claim[i:Nodes] ->
		   		if (i == I) then LEADER
				else if (i < I) then (send.claim[i] -> IDLE)
				else ELIGIBLE
		   | send.claim[I] -> ELIGIBLE)
+ {send.claim[Nodes]}.

CHANNEL = (token.sent -> token.received -> CHANNEL | claim.sent[i:Nodes] -> claim.received[i] -> CHANNEL).

|| ELECTIONRING = ([i:Nodes]:NODE(i) || [j:Nodes]:CHANNEL) / {
	[j:Nodes].send.claim[i:Nodes]/[j].claim.sent[i],
	[j:Nodes].recv.claim[i:Nodes]/[(j+N-1)%N].claim.received[i],
	[j:Nodes].send.token/[j].token.sent,
	[j:Nodes].recv.token/[(j+N-1)%N].token.received
}.

|| ELECTIONSERVICE = ELECTIONRING @ {[Nodes].open, [Nodes].close}.

minimal || ABSTRACT0 = ELECTIONRING.
minimal || ABSTRACT1 = ABSTRACT0 \ {[b:Nodes].send.claim[a:Nodes]}.
minimal || ABSTRACT2 = ABSTRACT1 \ {[b:Nodes].recv.claim[a:Nodes]}.
minimal || ABSTRACT3 = ABSTRACT2 \ {[b:Nodes].recv.token}.
minimal || ABSTRACT4 = ABSTRACT3 \ {[b:Nodes].send.token}.
