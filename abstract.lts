const N = 3
range Nodes= 0..(N-1)

NOMINALRING(I=N) = IDLE,
LEADER = ( open -> close -> send.token -> IDLE
		 | send.token -> IDLE),
IDLE = 	( recv.token -> LEADER
		| recv.claim[a:Nodes] -> if (a < I) then (send.claim[a] -> IDLE)
							 	 else IDLE
		| send.claim[I] -> ELIGIBLE),
ELIGIBLE = 	( recv.token -> LEADER
			| recv.claim[a:Nodes] -> if (I == a) then LEADER
								 else if (a < I) then (send.claim[a] -> IDLE)
								 else ELIGIBLE
			| send.claim[I] -> ELIGIBLE) + {send.claim[Nodes]}.

CHANNEL = (sent.token -> recvd.token -> CHANNEL | sent.claim.[a:Nodes] -> recvd.claim[a] -> CHANNEL).

|| ELECTIONRING = ([a:Nodes]:NOMINALRING(a) || [b:Nodes]:CHANNEL ) / {
	[b:Nodes].send.token/[b].sent.token,
	[b:Nodes].recv.token/[(b+N-1)%N].recvd.token,
	[b:Nodes].send.claim[a:Nodes]/[b].sent.claim[a],
	[b:Nodes].recv.claim[a:Nodes]/[(b+N-1)%N].recvd.claim[a]
}.

|| ELECTIONSERVICE = ELECTIONRING @ {[Nodes].open, [Nodes].close}.

minimal || ABSTRACT0 = ELECTIONRING.
minimal || ABSTRACT1 = ABSTRACT0 \ {[b:Nodes].send.claim[a:Nodes]}.
minimal || ABSTRACT2 = ABSTRACT1 \ {[b:Nodes].recv.claim[a:Nodes]}.
minimal || ABSTRACT3 = ABSTRACT2 \ {[b:Nodes].recv.token}.
minimal || ABSTRACT4 = ABSTRACT3 \ {[b:Nodes].send.token}.
