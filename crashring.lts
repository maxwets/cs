const N = 2
range Nodes = 0..(N-1)

NODE(I=N) = IDLE,
LEADER    = ( open -> close -> send.token -> IDLE
			| send.token -> IDLE),
IDLE 	  = ( recv.token -> LEADER
	   		| recv.claim[i:Nodes] ->
				if (i < I) then (send.claim[i] -> IDLE)
				else IDLE
	   		| send.claim[I] -> ELIGIBLE),
ELIGIBLE  = ( recv.token -> LEADER
		   	| recv.claim[i:Nodes] ->
		   		if (i == I) then LEADER
				else if (i < I) then (send.claim[i] -> IDLE)
				else ELIGIBLE
            | send.claim[I] -> ELIGIBLE),
CRASHED   = ( idle -> CRASHED
            | recv.claim[i:Nodes] ->
                if (i == I) then CRASHED
                else (send.claim[I] -> CRASHED)
            | recv.token -> send.token -> CRASHED)
+ {send.claim[Nodes]}.

CHANNEL = (token.sent -> token.received -> CHANNEL | claim.sent[i:Nodes] -> claim.received[i] -> CHANNEL).

|| CRASHRING = ([i:Nodes]:NODE(i) || [j:Nodes]:CHANNEL) / {
	[j:Nodes].send.claim[i:Nodes]/[j].claim.sent[i],
	[j:Nodes].recv.claim[i:Nodes]/[(j+N-1)%N].claim.received[i],
	[j:Nodes].send.token/[j].token.sent,
	[j:Nodes].recv.token/[(j+N-1)%N].token.received
}.

fluent CRITICAL[i:Nodes] = <{[i].open}, {[i].close}>
assert MUTEX = forall[i:Nodes] forall[j:Nodes] (rigid(i != j) -> []!(CRITICAL[i] && CRITICAL[j]))

fluent FIRSTCLAIM[i:Nodes] = <[i].send.claim[i], [i].recv.claim[i]>
assert NODUPCLAIM = forall[i:Nodes] (FIRSTCLAIM[i] -> X (!FIRSTCLAIM[i]))
assert NODUP_MUTEX = (NODUPCLAIM -> MUTEX)

assert ALLFAIR = [] <> [Nodes].open
assert NOCRASH = [] (! [Nodes].crash)
assert FAIR = [] <> [Nodes].open
assert NOCRASH_FAIR = (NOCRASH -> FAIR)
